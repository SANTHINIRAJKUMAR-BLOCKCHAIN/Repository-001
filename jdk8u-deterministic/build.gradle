repositories {
    maven {
        url "$artifactory_contextUrl/corda-releases"
    }
    maven {
        url "$artifactory_contextUrl/corda-dev"
    }
}

ext {
    jdk_home = "$projectDir/jdk".toString()
    rt_jar = "$jdk_home/jre/lib/rt.jar".toString()
}

configurations {
    jdk.resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
    }
}

dependencies {
    // Ensure everyone uses the latest SNAPSHOT.
    jdk "net.corda:deterministic-rt:latest.integration:api"
}

task copyVersion(type: Copy) {
    from processResources
    into jdk_home
    eachFile {
        if (it.sourcePath.startsWith("bin/")) {
            fileMode = 0755
        } else {
            fileMode = 0644
        }
    }
}

task copyJdk(type: Copy) {
    def jreDirectory = "$jdk_home/jre/lib"
    outputs.dir jreDirectory

    from(configurations.jdk.asPath) {
        rename 'deterministic-rt-(.*).jar', 'rt.jar'
    }
    into jreDirectory
}
copyJdk.finalizedBy copyVersion

assemble.dependsOn copyJdk
jar.enabled = false
