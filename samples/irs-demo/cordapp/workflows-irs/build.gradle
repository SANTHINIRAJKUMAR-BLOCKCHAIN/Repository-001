apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'net.corda.plugins.cordapp'

configurations {
//    integrationTestCompile.extendsFrom testCompile
//    integrationTestRuntime.extendsFrom testRuntime
    demoArtifacts.extendsFrom testRuntime
}

dependencies {
    // The irs demo CorDapp depends upon Cash CorDapp features
    cordapp project(':finance:contracts')
    cordapp project(':finance:workflows')

    // Corda integration dependencies
    cordaCompile project(':core')

    // Cordapp dependencies
    // Specify your cordapp's dependencies below, including dependent cordapps
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    testCompile project(':node-driver')
    testCompile "junit:junit:$junit_version"
    testCompile "org.assertj:assertj-core:${assertj_version}"

    cordapp project(':samples:irs-demo:cordapp:contracts-irs')
}

cordapp {
    targetPlatformVersion corda_platform_version.toInteger()
    minimumPlatformVersion 1
    workflow {
        name "Corda IRS Demo - Flows"
        versionId 1
        vendor "R3"
        licence "Open Source (Apache 2)"
    }
}

jar {
    baseName 'corda-irs-demo-workflows'
}

task testJar(type: Jar) {
    classifier "test"
    from sourceSets.main.output
    from sourceSets.test.output
}

artifacts {
    demoArtifacts testJar
}