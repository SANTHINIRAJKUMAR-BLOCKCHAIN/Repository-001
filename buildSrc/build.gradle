buildscript {
    Properties constants = new Properties()
    file("../constants.properties").withInputStream { constants.load(it) }

    ext {
        guava_version = constants.getProperty("guavaVersion")
        class_graph_version = constants.getProperty('classgraphVersion')
        assertj_version = '3.9.1'
        junit_version = '4.12'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

allprojects {
    tasks.withType(Test) {
        // Prevent the project from creating temporary files outside of the build directory.
        systemProperty 'java.io.tmpdir', buildDir.absolutePath
    }
}

configurations {
    runtime
}

dependencies {
    // Add the top-level projects ONLY to the host project.
    runtime project.childProjects.collect { n, p ->
        project(p.path)
    }
    compile gradleApi()
    compile "io.fabric8:kubernetes-client:4.4.1"
    compile 'org.apache.commons:commons-compress:1.19'
    compile 'commons-codec:commons-codec:1.13'
    compile "io.github.classgraph:classgraph:$class_graph_version"
    compile "com.bmuschko:gradle-docker-plugin:5.0.0"
    testCompile "junit:junit:$junit_version"
}
