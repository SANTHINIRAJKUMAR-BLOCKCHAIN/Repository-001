apply plugin: 'kotlin'
apply plugin: 'idea'

evaluationDependsOn(':jdk8u-deterministic')

def jdkTask = project(':jdk8u-deterministic').assemble
def deterministic_jdk_home = project(':jdk8u-deterministic').jdk_home

dependencies {
    compileOnly project(path: ':core-deterministic', configuration: 'runtimeArtifacts')
    compileOnly project(path: ':serialization-deterministic', configuration: 'runtimeArtifacts')
    compileOnly "junit:junit:$junit_version"
}

tasks.withType(AbstractCompile) {
    dependsOn jdkTask
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-bootclasspath' << "$deterministic_jdk_home/jre/lib/rt.jar".toString()
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions.jdkHome = deterministic_jdk_home
}

idea {
    module {
        if (project.hasProperty("deterministic_idea_sdk")) {
            jdkName project.property("deterministic_idea_sdk") as String
        }
    }
}
