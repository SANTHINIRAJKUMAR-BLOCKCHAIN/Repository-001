buildscript {
    ext.tornadofx_version = '1.6.2'
    ext.jna_version = '4.1.0'
    ext.purejavacomm_version = '0.0.17'
    ext.guava_version = '14.0.1'
    ext.slf4j_version = '1.7.22'
    ext.controlsfx_version = '8.40.12'

    ext.java_home = System.properties.'java.home'
    ext.pkg_source = "$buildDir/packagesrc"
    ext.pkg_outdir = "$buildDir/javapackage"
    ext.dist_source = "$pkg_source/demobench-$version"
    ext.pkg_version = "$version".indexOf('-') >= 0 ? "$version".substring(0, "$version".indexOf('-')) : version

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

evaluationDependsOn(':tools:explorer:capsule')

mainClassName = 'net.corda.demobench.DemoBench'
applicationDefaultJvmArgs = ['-Djava.util.logging.config.class=net.corda.demobench.config.LoggingConfig', '-Dorg.jboss.logging.provider=slf4j']

repositories {
    flatDir {
        dirs 'libs'
    }

    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url 'http://www.sparetimelabs.com/maven2'
    }
    maven {
        url 'https://dl.bintray.com/kotlin/exposed'
    }
}

dependencies {
    // TornadoFX: A lightweight Kotlin framework for working with JavaFX UI's.
    compile group: 'no.tornado', name: 'tornadofx', version: tornadofx_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version

    // Controls FX: more java FX components http://fxexperience.com/controlsfx/
    compile group: 'org.controlsfx', name: 'controlsfx', version: controlsfx_version

    // ONLY USING THE RPC CLIENT!?
    compile (project(':node')) {
        exclude module: 'commons-logging'
        exclude module: 'kotlin-test'
        exclude module: 'junit'
    }

    compile group: 'com.h2database', name: 'h2', version: h2_version
    compile group: 'net.java.dev.jna', name: 'jna-platform', version: jna_version
    compile group: 'com.google.guava', name: 'guava', version: guava_version
    compile group: 'com.sparetimelabs', name: 'purejavacomm', version: purejavacomm_version
    compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: slf4j_version
    compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: slf4j_version
    compile group: 'org.slf4j', name: 'jul-to-slf4j', version: slf4j_version
    compile group: 'com.typesafe', name: 'config', version: typesafe_config_version

    // These libraries don't exist in any Maven repository I can find.
    // See: https://github.com/JetBrains/jediterm
    //
    // The terminal JAR here has also been tweaked:
    // See: https://github.com/JetBrains/jediterm/issues/144
    compile ':jediterm-terminal-2.5'
    compile ':pty4j-0.7.2'

    testCompile group: 'junit', name: 'junit', version: junit_version
    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test', version: kotlin_version
}

compileKotlin {
    kotlinOptions.jvmTarget = 1.8
}

jar {
    manifest {
        attributes(
            'Corda-Version': corda_version,
            'Main-Class': mainClassName,
            'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
        )
    }
}

test {
    systemProperty 'java.util.logging.config.class', 'net.corda.demobench.config.LoggingConfig'
    systemProperty 'org.jboss.logging.provider', 'slf4j'
}

distributions {
    main() {
        contents {
            into('lib/linux') {
                from 'libs/linux'
            }
            into('lib/macosx') {
                from 'libs/macosx'
            }
            into('lib/win') {
                from 'libs/win'
            }
            from(project(':tools:explorer:capsule').tasks.buildExplorerJAR) {
                rename 'node-explorer-(.*)', 'node-explorer.jar'
                into 'explorer'
            }
            from(project(':node:capsule').tasks.buildCordaJAR) {
                rename 'corda-(.*)', 'corda.jar'
                into 'corda'
            }
            from(project(':node:webserver').tasks.buildWebserverJar) {
                rename 'corda-webserver-(.*)', 'corda-webserver.jar'
                into 'corda'
            }
            from(project(':samples:bank-of-corda-demo').jar) {
                rename 'bank-of-corda-demo-(.*)', 'bank-of-corda.jar'
                into 'plugins'
            }
       }
    }
}

/*
 * Bundles the application using JavaPackager,
 * using the ZIP distribution as source.
 */
task javapackage(dependsOn: 'distZip') {

    doLast {
        delete([pkg_source, pkg_outdir])

        copy {
            from(zipTree(distZip.outputs.files.singleFile))
            into pkg_source
        }

        copy {
            /*
             * Copy non-text formats "as-is".
             */
            from("$projectDir/package") {
                exclude '**/*.spec'
                exclude '**/*.sh'
                exclude '**/*.wsf'
                exclude '**/*.manifest'
            }
            into "$pkg_source/package"
        }

        copy {
            /*
             * Expand tokens for text formats.
             */
            from("$projectDir/package") {
                include '**/*.spec'
                include '**/*.sh'
                include '**/*.wsf'
                include '**/*.manifest'
            }
            filter {
                line -> line.replaceAll('@pkg_version@', pkg_version)
            }
            into "$pkg_source/package"
        }

        ant.taskdef(
            resource: 'com/sun/javafx/tools/ant/antlib.xml',
            classpath: "$pkg_source:$java_home/../lib/ant-javafx.jar"
        )

        ant.deploy(nativeBundles: packageType, outdir: pkg_outdir, outfile: 'DemoBench', verbose: 'true') {
            application(name: 'DemoBench', version: pkg_version, mainClass: mainClassName)
            info(title: 'DemoBench', vendor: 'R3', description: 'A sales and educational tool for demonstrating Corda.')
            resources {
                fileset(dir: "$dist_source/lib", type: 'jar') {
                    include(name: '*.jar')
                }

                fileset(dir: "$dist_source/lib", type: 'native') {
                    include(name: "macosx/**/*.dylib")
                    include(name: "win/**/*.dll")
                    include(name: "win/**/*.exe")
                    include(name: "linux/**/*.so")
                }

                fileset(dir: dist_source, type: 'data') {
                    include(name: 'corda/*.jar')
                    include(name: 'plugins/*.jar')
                    include(name: 'explorer/*.jar')
                }
            }

            platform {
                property(name: 'java.util.logging.config.class', value: 'net.corda.demobench.config.LoggingConfig')
                property(name: 'org.jboss.logging.provider', value: 'slf4j')
            }

            preferences(install: false)
        }
    }
}
